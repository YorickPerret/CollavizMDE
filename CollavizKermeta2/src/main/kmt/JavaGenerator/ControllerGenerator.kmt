using kermeta::standard::*
using kermeta::io::StdIO => stdio
using kermeta::io::FileIO
using ecore::*
using Helper::*

package JavaGenerator{
	class ControllerGenerator inherits Generator, GenericGenerator
	{

		method generate(p : EPackage) : Void is do 
			// TODO: implement 'myOperation' operation
			stdio.writeln("\n\n******** Generating controllers ********\n\n")
			var helper : CollavizHelper init CollavizHelper.new
			helper.initAuthorizedClasses()
			p.eClassifiers.each {c |
				if helper.isAuthorizedClasse(c.name) then
					var clazz : EClass
					clazz?=c
					if clazz != void then
						//controllers
						generateControllersClasses(clazz)
						
						//interfaces
						generateInterfaceClasses(clazz)
						
					end
				end
			}			
			stdio.writeln("Generating controllers finish")
		end
		
		operation generateControllersClasses(clazz : EClass) : Void is do
			//generating classes
			var bufferClassClient : StringBuffer init StringBuffer.new
			var bufferClassServer : StringBuffer init StringBuffer.new
			
			stdio.writeln("******** Generating classe "+clazz.name+" ********")
			var superType : EClass init clazz.eSuperTypes.one
			bufferClassClient.append(generateImportClient(clazz))
			bufferClassServer.append(generateImportServer(clazz))
			bufferClassClient.append(generateBeginClasseClient(clazz,superType))
			bufferClassServer.append(generateBeginClasseServer(clazz,superType))
			
			/* /!\ add operations /!\  */	
			bufferClassClient.append(generateConstructorClient(clazz))
			bufferClassServer.append(generateConstructorServer(clazz))	
			
			//essai sur les operations
			clazz.eOperations.each { o |
				bufferClassClient.append(generateOperation(o))
				bufferClassServer.append(generateOperation(o))
			}			
			
			var tmp : String init generateEndClasse()
			bufferClassClient.append(tmp)
			bufferClassServer.append(tmp)
			stdio.writeln("generating classe done")
			
			//save files
			stdio.writeln("Saving controller")
			
			var resCClient : String init "platform:/resource/CollavizKermeta2/src/main/JavaCode/Controller/CClient_"+clazz.name+".java"
			FileIO.writeTextFile(resCClient.toURI(),bufferClassClient.toString())
			var resCServer : String init "platform:/resource/CollavizKermeta2/src/main/JavaCode/Controller/CService_"+clazz.name+".java"
			FileIO.writeTextFile(resCServer.toURI(),bufferClassServer.toString())
			
			stdio.writeln("******** Save succes ********\n")
		end
		
		operation generateImportClient(clazz : EClass) : String is do
			var buffer : StringBuffer init StringBuffer.new
			buffer.append("package org.collaviz.iivc.control.client;\n")
			buffer.append("import org.collaviz.collaboration.objects.abstraction.IA_SharedObject ;\n")
			buffer.append("import org.collaviz.collaboration.objects.control.client.CClient_ObjectManager ;\n")
			buffer.append("import org.collaviz.iivc.control.IC_"+clazz.name+" ;\n\n")
			result?=buffer.toString()
		end
		
		operation generateImportServer(clazz : EClass) : String is do
			var buffer : StringBuffer init StringBuffer.new
			buffer.append("package org.collaviz.iivc.control.service;\n")
			buffer.append("import org.collaviz.collaboration.objects.abstraction.IA_SharedObject ;\n")
			buffer.append("import org.collaviz.collaboration.objects.control.service.CService_ObjectManager ;\n")
			buffer.append("import org.collaviz.iivc.control.IC_"+clazz.name+" ;\n\n")
			result?=buffer.toString()
		end		
		operation generateInterfaceClasses(clazz : EClass) : Void is do
		
			var bufferInterface : String
		
			//generating interfaces
			stdio.writeln("******** Generating Interface "+clazz.name+" ********")
			bufferInterface ?= generateInterface(clazz)
			stdio.writeln("generating interface done")
			
			//save file
			stdio.writeln("Saving interface controllers")
			var resCInterface : String init "platform:/resource/CollavizKermeta2/src/main/JavaCode/Controller/IC_"+clazz.name+".java"
			FileIO.writeTextFile(resCInterface.toURI(),bufferInterface)
			stdio.writeln("******** Save succes ********\n")
		end
		
		method generateBeginClasse(eclass : EClass , esuperclass : EClass) : String is do
			result?=""
		end
		
		operation generateBeginClasseClient(eclass : EClass , esuperclass : EClass) : String is do
			result?="public class CClient_"+eclass.name+" extends CClient_"+esuperclass.name+" implements IC_"+eclass.name+" {\n\n"
		end
		
		operation generateBeginClasseServer(eclass : EClass , esuperclass : EClass) : String is do
			result?="public class CService_"+eclass.name+" extends CService_"+esuperclass.name+" implements IC_"+eclass.name+" {\n\n"
		end
		
		method generateInterface(clazz : EClass) : String is do
			var bufferInterface : StringBuffer init StringBuffer.new			
			bufferInterface.append("package org.collaviz.iivc.control\n\n")
			bufferInterface.append("import org.collaviz.iivc.abstraction.IA_"+clazz.name+"\n\n")
			bufferInterface.append("public interface IC_"+clazz.name+" extends IA_"+clazz.name+"{\n")
			
			bufferInterface.append("\n}")
			result?=bufferInterface.toString()
		end
		
		method generateConstructor(eclass : EClass) : String is do
			result?=""
		end
		
		operation generateConstructorClient(eclass : EClass) : String is do
			var buffer : StringBuffer init StringBuffer.new
			buffer.append("	public CClient_"+eclass.name+"(IA_SharedObject abstraction, boolean referentProxyArchi, int accessLevel, CClient_ObjectManager objectManager) {\n")
			buffer.append("		super (abstraction, referentProxyArchi, accessLevel, objectManager) ;\n")
			buffer.append("	}\n\n")
			result?=buffer.toString()
		end
		
		operation generateConstructorServer(eclass : EClass) : String is do
			var buffer : StringBuffer init StringBuffer.new
			buffer.append("	public CService_"+eclass.name+"(IA_SharedObject abstraction, boolean referentProxyArchi, int accessLevel, CService_ObjectManager objectManager) {\n")
			buffer.append("		super (abstraction, referentProxyArchi, accessLevel, objectManager) ;\n")
			buffer.append("	}\n\n")
			result?=buffer.toString()
		end
		
		method generateOperation(o : EOperation) : String is do
			var buffer : StringBuffer init StringBuffer.new
			buffer.append("	@Override\n")
			buffer.append("	public "+o.eType.name+" "+ o.name+" (")
			//parameters
			o.eParameters.each{ p |
				if p.upperBound != 1 then
					buffer.append(p.eType.name+"[] "+p.name)
				else
					buffer.append(p.eType.name+" "+p.name)
				end
			}
			buffer.append(")\n")
			buffer.append("		callModificationMethod (\""+o.name+"\"")
			//parameters for callModificationMethod
			o.eParameters.each{ p |
				buffer.append(","+p.name)
			}
			
			buffer.append(")\n")
			buffer.append("	}\n")
			   
   
			result?=buffer.toString()			
		end
		
		
	}
}